apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

project.ext {
    apacheZooKeeperVersion = '3.8.4'
    googleFindbugsVersion = '3.0.1'
    hamcrestVersion = '1.3'
    junitVersion = '4.11'
    slf4jVersion = '1.7.21'
}

configurations.all {
    resolutionStrategy {
        force "org.hamcrest:hamcrest-all:$hamcrestVersion"
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://packages.confluent.io/maven/' }
}

dependencies {
    compile (
            "com.google.code.findbugs:annotations:$googleFindbugsVersion",
            "org.apache.zookeeper:zookeeper:$apacheZooKeeperVersion",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "io.dropwizard.metrics:metrics-core:4.1.12.1",
            "org.xerial.snappy:snappy-java:1.1.10.5",
    )

    testCompile (
            "com.google.code.findbugs:annotations:$googleFindbugsVersion",
            "junit:junit:$junitVersion",
    )
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

compileTestJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

checkstyle {
    toolVersion = "7.1.2"
}

findbugs {
    toolVersion = "3.0.1"
}

// findbugs html only report
findbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

test {
    forkEvery = 1 // SASL tests must run in separate JVMs due to JAAS config loading.
}

jar {
    manifest {
        attributes 'Implementation-Title': 'WePay ZKTools library',
                   'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'ZkTools'
                description = 'ZooKeeper tools and utilities'
                url = 'https://www.github.com/wepay/zktools'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ymatsuda'
                        name = 'Yasuhiro Matsuda'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wepay/zktools.git'
                    developerConnection = 'scm:git:ssh://github.com/wepay/zktools.git'
                    url = 'https://github.com/wepay/zktools'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty('ossrhUsername')?: ''
                password findProperty('ossrhPassword')?: ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
